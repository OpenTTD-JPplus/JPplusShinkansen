
switch (FEAT_TRAINS, SELF, sw_100_series_articulated_part, extra_callback_info1) {
		1 .. 2: return series_100;									
		return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
	}

/* SPEED */
    switch(FEAT_TRAINS, SELF, sw_100_series_speed, cargo_subtype) {
        0..2: return 220;   // Original speed
        return 230;         // Kodama speed
    }

item(FEAT_TRAINS, series_100) {
	property {
		/* Menu */
        name: 							string(STR_NAME_100_SERIES);
		sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
        misc_flags:                     bitmask(TRAIN_FLAG_2CC, TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_SPRITE_STACK);
		running_cost_base:              RUNNING_COST_ELECTRIC;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
        /* Availability */
        climates_available:             ALL_CLIMATES;
		introduction_date:              date(1985,10,01);           // Date of launch
		model_life:                     1993 - 1985;		        // Constructed until 1992
		vehicle_life:                   30;							// 
		track_type:                     SHINKANSEN;
        /* Cargo */
        reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
		ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;							// Changed by callback
		loading_speed:                  50;							
		cargo_age_period:               278; 						// 1.5 times normal for this expensive long-distance train		
		/* Stats */
        speed:                          211 km/h;                   // Changed by callback
		power:                          8050 kW;					// Changed by callback
		weight:                         1464 ton;                   // Changed by callback					
		tractive_effort_coefficient:    0.3;
		air_drag_coefficient:			0;
	}
	graphics {
		/* Menu */
        additional_text:                string(STR_DESC_100_SERIES);
        cargo_subtype_text:             sw_100_series_year_dep_subtype_text;
		can_attach_wagon:				sw_attach_mu_car_full;
		start_stop:						sw_stop_start_full_length_4;
		purchase_weight:                53;
        purchase_power:                 308 * 4;
        /* Graphics */
        default:                        sw_100_series_graphics;
		purchase:                       sw_100_series_purchase;
		articulated_part:               sw_100_series_articulated_part;
		visual_effect_and_powered:      sw_visual_effect_and_powered;
        /* Stats */
        length:                         sw_length_1_8_1;
        weight:                         NumberCarsInConsist() * 53;
        power:                          max (NumberCarsInConsist() - 2 ,0) * 308 * 4;	// 230 kW
        speed:							sw_100_series_speed;
		cargo_capacity:                 27 * CapacityFactor();															
		loading_speed:					27 * CapacityFactor() / param_loading_2D;					// Capacity divided by door loading factor
		cargo_age_period:				param_decay_express;
		cost_factor:                    132;
		running_cost_factor:            (300 + 123 * NumberCarsInConsist() ) * RunningCostFactor();
		purchase_running_cost_factor:   300 + 123 * NumberCarsInConsist();
		
	}
	/*Multiple Unit Car*/
	
      livery_override(mu_car_full){
        default:                        sw_100_series_graphics;
        cargo_capacity:                 27 * CapacityFactor();	
        cargo_age_period:				param_decay_express;
        loading_speed:					27 * CapacityFactor() / param_loading_2D;
		articulated_part:				sw_mucar_full_articulated_part;
		length:							sw_length_1_8_1;
		//power:						// Apply power in graphics block to calculate power based on number of motorised cars correctly
		//weight:						// Apply weight to graphics block to calculate Max T.E. correctly
        //running_cost_factor:            sw_100_series_running_costs;
      }
}
