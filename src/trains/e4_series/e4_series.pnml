/* Begin E4 Series */

#include "src/trains/e4_series/e4_series_subtypes.pnml"
#include "src/trains/e4_series/e4_series_gfx.pnml"
#include "src/trains/e4_series/e4_series_liveries.pnml"

// Panto car positions when reversed
switch (FEAT_TRAINS, SELF, sw_e4_series_backwards_standard, position_in_consist_from_end % 24) {				
		10: ss_e4_series_panto_rev;
		16: ss_e4_series_panto_rev;
		ss_e4_series_coach_rev;				
	}

switch (FEAT_TRAINS, SELF, sw_e4_series_backwards_special, position_in_consist_from_end % 48) {				
		10: ss_e4_series_panto_rev;
		16: ss_e4_series_panto_rev;
		22: ss_e4_series_cab;
		25: ss_e4_series_cab_rev;
		34: ss_e4_series_panto_rev;
		40: ss_e4_series_panto_rev;
		ss_e4_series_coach_rev;				
	}	

// Panto car positions
switch (FEAT_TRAINS, SELF, sw_e4_series_forwards_standard, position_in_consist % 24) {				
		10: ss_e4_series_panto;
		16: ss_e4_series_panto;
		ss_e4_series_coach;				
	}

switch (FEAT_TRAINS, SELF, sw_e4_series_forwards_special, position_in_consist % 48) {				
		10: ss_e4_series_panto;
		16: ss_e4_series_panto;
		22: ss_e4_series_cab_rev;
		25: ss_e4_series_cab;
		34: ss_e4_series_panto;
		40: ss_e4_series_panto;
		ss_e4_series_coach;				
	}

/* SPECIAL - */
switch (FEAT_TRAINS, SELF, sw_e4_series_forwards, num_vehs_in_consist == 48) {				
		0:  sw_e4_series_forwards_standard;
		sw_e4_series_forwards_special;				
	}

switch (FEAT_TRAINS, SELF, sw_e4_series_backwards, num_vehs_in_consist == 48) {				
		0:  sw_e4_series_backwards_standard;
		sw_e4_series_backwards_special;				
	}

// Is the Shinkansen reversed?
switch (FEAT_TRAINS, PARENT, sw_e4_series_midcars, vehicle_is_reversed) {				
		0:  sw_e4_series_forwards;
		sw_e4_series_backwards;				
	}

// Rear Car Selection
switch (FEAT_TRAINS, SELF, sw_e4_series_back, position_in_consist_from_end) {				
		1:  ss_e4_series_cab_rev;
		sw_e4_series_midcars;				
	}

// Front Cab selection
switch (FEAT_TRAINS, SELF, sw_e4_series_front, position_in_consist) {				
		1:  ss_e4_series_cab;
		sw_e4_series_back;				
	}

// GRAPHICS
switch (FEAT_TRAINS, SELF, sw_e4_series_graphics, position_in_consist % 3) {				
		1:  sw_e4_series_front; 				
		dummy_sprites;
	}

switch (FEAT_TRAINS, SELF, sw_e4_series_articulated_part, extra_callback_info1) {
		1 .. 2: return series_e4;									
		return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
	}

/*---Coupling Switch---*/
switch(FEAT_TRAINS, SELF, sw_e4_series_attach_vehid, [vehicle_type_id == mu_car_full] ) {
 0: return string(ONLY_ADD_MUCAR);
}

/*---Length Check---*/
switch (FEAT_TRAINS, SELF, sw_e4_series_stop_start, [num_vehs_in_consist < 12]) {				
		1:  return string(STR_LENGTH_ERROR); 				
		return CB_RESULT_NO_TEXT; 
	}

item(FEAT_TRAINS, series_e4) {
	property {
		/* Menu */
        name: 							string(STR_NAME_E4_SERIES);
		sprite_id:                      SPRITE_ID_NEW_TRAIN; 		// required to enable new graphics
        misc_flags:                     bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_TILT, TRAIN_FLAG_AUTOREFIT);
        running_cost_base:              RUNNING_COST_ELECTRIC;
		engine_class:                   ENGINE_CLASS_ELECTRIC;
        /* Availability */
        climates_available:             ALL_CLIMATES;
		introduction_date:              date(1997,12,20);
		model_life:                     VEHICLE_NEVER_EXPIRES;		//
		vehicle_life:                   30;							// 
		track_type:                     SHINKANSEN;
        /* Cargo */
        reliability_decay:              5;
		refittable_cargo_classes:       bitmask(CC_PASSENGERS);
		refit_cost:                     0;
		default_cargo_type:             PASS;
		ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 1;							// 
		loading_speed:                  50;							
		cargo_age_period:               278; 						// 1.5 times normal for this expensive long-distance train
		/* Stats */
		speed:                          241 km/h;					// Operational Top Speed
		power:                          560 * 4 * 8 hp;				// Agreed to Wikipedia
		weight:                         54 * 8 tons;				// Agreed to Wikipedia 
		tractive_effort_coefficient:    0.3;
		air_drag_coefficient:			0;
	}
	graphics {
		/* Menu */
        additional_text:                string(STR_DESC_E4_SERIES);
        cargo_subtype_text:             sw_e4_series_year_dep_subtype_text;
		can_attach_wagon:				sw_e4_series_attach_vehid;
		start_stop:						sw_e4_series_stop_start;
        /* Graphics */
        default:                        sw_e4_series_graphics;
		purchase:                       sw_e4_series_year_dep_purchase;
		articulated_part:               sw_e4_series_articulated_part;
		visual_effect_and_powered:      sw_visual_effect_and_powered;
        /* Stats */
        length:                         sw_length_2_8_2;
		power:							NumberCarsInConsist() / 2 * 563 * 4;	// 420 kW = 563 hp
		weight:							NumberCarsInConsist() * 54;
		cargo_capacity:                 35 * CapacityFactor();										// 817 capacity / (8 cars * 3 parts) is approx 35						
		loading_speed:					35 * CapacityFactor() / param_loading_2D;					// Capacity divided by door loading factor
		cargo_age_period:				param_decay_express;
		cost_factor:                    105;
        running_cost_factor:            (295 + 120 * NumberCarsInConsist() ) * RunningCostFactor();
		purchase_running_cost_factor:   295 + 120 * NumberCarsInConsist();
	}

	/*Multiple Unit Car*/
	
      livery_override(mu_car_full){
        default:                        sw_e4_series_graphics;
        cargo_capacity:                 35 * CapacityFactor();
        cargo_age_period:				param_decay_express;
        loading_speed:                  35 * CapacityFactor() / param_loading_2D;
		articulated_part:				sw_mucar_full_articulated_part;
		length:							sw_length_2_8_2;
		//power:						// Apply power in graphics block to calculate power based on number of motorised cars correctly
        //weight:						// Apply weight in graphics block to calculate Max T.E. correctly
        //running_cost_factor:            sw_e4_series_running_costs;
      }
}

/* End E4 series */
